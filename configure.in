dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/queue.c)

cat <<'EOF'

**********************
*                    *
* Configuring REMIND *
*                    *
**********************

EOF

AC_CONFIG_HEADER(src/config.h)

AC_ARG_ENABLE(perl-build-artifacts,
[  --disable-perl-build-artifacts
                          Disable perllocal.pod and .packlist generation], ac_cv_perlartifacts=$enableval, ac_cv_perlartifacts=yes)

AH_BOTTOM([#include <custom.h>])

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PATH_PROG([PERL], [perl])

dnl Checks for libraries.
AC_CHECK_LIB(m, sqrt)

dnl Integer sizes
AC_CHECK_SIZEOF(unsigned int)
AC_CHECK_SIZEOF(unsigned long)

dnl Checks for header files.
AC_CHECK_HEADERS(sys/types.h sys/file.h glob.h wctype.h locale.h langinfo.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_STRUCT_TM

dnl Checks for library functions.
AC_FUNC_UTIME_NULL
AC_HEADER_TIME

if test "$GCC" = yes; then
	CFLAGS="$CFLAGS -Wall -Wextra -Wstrict-prototypes"
        # Check for link-time optimization support
        for f in -flto=auto -ffat-lto-objects; do
            AC_MSG_CHECKING([whether $CC supports $f])
            if $CC -E $f /dev/null > /dev/null 2>&1 ; then
                AC_MSG_RESULT([yes])
                CFLAGS="$CFLAGS $f"
            else
                AC_MSG_RESULT([no])
            fi
        done
fi

if test "$ac_cv_perlartifacts" = "yes" ; then
    PERLARTIFACTS=
else
    PERLARTIFACTS='NO_PACKLIST=1 NO_PERLLOCAL=1'
fi

RELEASE_DATE=`grep '[[0-9]][[0-9]][[0-9]][[0-9]]-[[0-9]][[0-9]]-[[0-9]][[0-9]]' docs/WHATSNEW  | head -n 1 | awk '{print $NF}'`

# Sanity-check release date
echo "$RELEASE_DATE" | grep '^....-..-..$' > /dev/null 2>&1
if test "$?" != 0 ; then
    echo "*** COULD NOT DETERMINE RELEASE DATE: docs/WHATSNEW is incorrect!"
    exit 1
fi
AC_CHECK_FUNCS(setenv unsetenv glob mbstowcs setlocale initgroups)
VERSION=04.01.00
AC_SUBST(VERSION)
AC_SUBST(PERL)
AC_SUBST(PERLARTIFACTS)
AC_SUBST(RELEASE_DATE)
AC_OUTPUT(src/Makefile www/Makefile src/version.h rem2html/Makefile rem2pdf/Makefile.PL rem2pdf/Makefile.top rem2pdf/bin/rem2pdf man/rem.1 man/rem2ps.1 man/remind.1 man/tkremind.1)
chmod a+x rem2pdf/bin/rem2pdf
